@NAME CandleFW_WICKUP_Parser

@EXT md

@IGNORE θws θnl

@SYMBOL ``` `` === </ ]()

@IMPORT ./html.hcg as html

+> html::TAG_BODY_ITEM → WICKUP_PLUGIN 

+> html::TEXT_NODE → WICKUP_INLINE_ELEMENT

<> WICKUP_PLUGIN → PRE_MD_BLK WICKUP_BLOCK 	↦return { $sym2 }
   │ PRE_MD_BLK html::TEXT_NODE				↦return { $sym2 }
   │ PRE_MD_BLK html::TAG 					↦return { $sym2 }
   │ PRE_MD_BLK								↦cstr ^text
   │ PRE_MD_BLK HORIZONTAL_RULE 			↦return { $sym2 }

<> PRE_MD_BLK → θnl θws(*)					↦r {$sym1}					

<> WICKUP_BLOCK → HEADER 
   │ (RED θnl) BLOCK_QUOTES				
   │ (RED θnl) CODE_BLOCK					
   │ (RED θnl) UNORDERED_LIST_ITEM

<> WICKUP_COMBO → html::TEXT_NODE (RED </ ) │ html::TAG 

<> WICKUP_COMBO_PLUS → WICKUP_COMBO │ HEADER

# Escaped symbol
# +> html::BODY_SYMBOLS → \\ θany 															↦r { $sym2 }

<> HORIZONTAL_RULE → HORIZONTAL_RULE_BODY													↦r { env.fn.element_selector("hr", null, null,  env, pos ) }
	│ HORIZONTAL_RULE_BODY WICKUP_COMBO														↦return { $sym2 }

<> HORIZONTAL_RULE_BODY → \_(+)																↦return { $sym1 }

<> HEADER → \#(+) 	 WICKUP_COMBO(+) 														↦r { env.fn.element_selector("h" + Math.min($sym1.length, 6), null, $$sym2,  env, pos) }

<> BLOCK_QUOTES → ( \> )(+)  WICKUP_COMBO_PLUS(+) 											↦r { env.fn.element_selector("blockquote", null, $sym2, env, pos, $sym1.length) }

<> UNORDERED_LIST_ITEM → ( \+  │ \*  )(+) WICKUP_COMBO_PLUS(+)  
																							
																							↦r { env.fn.element_selector("li", null, $sym2, env, pos, $sym1.length) }
				

<> WICKUP_INLINE_ELEMENT → LINK
	│ ITALIC_BOLD_EMPHASIS
	│ CODE_QUOTE		

<> CODE_BLOCK → \``` θid?   θnl (θnl? html::TEXT_NODE 
										↦r { env.fn.element_selector("code", null,[($sym2.data = $sym1 + $sym2.data, $sym2)], env, pos)} )(+) (IGN θws θnl) \```  
																							
																							↦r { env.fn.element_selector("pre", null, $$sym4, env, pos) }

<> LINK → \[ (IGN θws) html::ATTRIBUTE_BODY (IGN θws) \]( WICKUP_COMBO \) 					↦r { env.fn.element_selector("a", [new env.fn.attribute(["href", "", $sym2])], [$sym5], env, pos) }


<> ITALIC_BOLD_EMPHASIS → \{ \{ \{ html::TEXT_NODE(+) \} \} \}								↦r { env.fn.element_selector("i", null, [env.fn.element_selector("b", null, $sym4, env, pos)], env, pos) }
	│ BOLD_EMPHASIS

<> BOLD_EMPHASIS → \{ \{ html::TEXT_NODE(+) \} \}											↦r { env.fn.element_selector("b", null, $sym3, env, pos) }
	│ EMPHASIS

<> EMPHASIS → \{ html::TEXT_NODE(+) \}														↦r { env.fn.element_selector("i", null, $sym2, env, pos) }


<> CODE_QUOTE → \`` CODE_QUOTE_SYMBOLS \`` 													↦r { env.fn.element_selector("code", null,  [new env.fn.text([$$sym2], env)], env, pos) }

<> MD_TEXT_NODE → BODY_SYMBOLS2(+") 														↦cstr ^text
	│ WICKUP_INLINE_ELEMENT

<> CODE_QUOTE_SYMBOLS → BODY_SYMBOLS2(+")

<> BODY_SYMBOLS2 → θid │ θsym │  θob │ θcb │ θop │ θnum │ θstr │ \#
